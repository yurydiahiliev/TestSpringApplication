plugins {
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'io.freefair.lombok' version '8.6'
    id 'com.google.cloud.tools.jib' version '3.4.1'
    id 'java'
}

ext {
    springdocOpenapiStarterWebMvcUiVersion = '2.2.0'
    r2dbcVersion = '1.0.4.RELEASE'
    flywayDbVersion = '10.9.1'
    jaxbApiVersion = '2.3.1'
    mapStructVersion = '1.5.5.Final'
    jsonWebTokenVersion = '0.9.1'
    dockerImage = System.getProperty("os.arch") == "arm64" ? "arm64v8/eclipse-temurin:19-jre" : "eclipse-temurin:19-jre"
    dockerArch = System.getProperty("os.arch") == "arm64" ? "arm64" : "amd64"
    registry = 'yudyagilev'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocOpenapiStarterWebMvcUiVersion"
    implementation 'org.postgresql:postgresql'
    implementation "org.postgresql:r2dbc-postgresql:$r2dbcVersion"
    implementation "org.flywaydb:flyway-core:$flywayDbVersion"
    runtimeOnly "org.flywaydb:flyway-database-postgresql:$flywayDbVersion"
    implementation "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    implementation "org.mapstruct:mapstruct:$mapStructVersion"
    implementation "io.jsonwebtoken:jjwt:$jsonWebTokenVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
    systemProperties = System.properties
}

tasks.jar {
    manifest {
        attributes["Main-Class"] = "com.posts.Application"
    }
}

jib {
    container {
        ports = ['8080', '5432', '80']
//        jvmFlags = ["-Dspring.profiles.active=docker"]
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
    from {
        image = "${project.ext.dockerImage}"
        platforms {
            platform {
                architecture = "${project.ext.dockerArch}"
                os = 'linux'
            }
        }
    }
    to {
        image = "${project.ext.registry}/${project.name}"
        tags = ['latest', "${project.version}"]
    }
}

tasks.named('test') {
    useJUnitPlatform()
}